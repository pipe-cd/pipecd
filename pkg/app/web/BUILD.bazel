load("@npm//webpack-cli:index.bzl", webpack = "webpack_cli")
load("@npm//@bazel/typescript:index.bzl", "ts_project")
load(":jest.bzl", "jest_test")

genrule(
    name = "build_api",
    srcs = glob(["api_client/*"]),
    outs = ["api_client"],
    cmd = "mkdir $(OUTS) && cp $(SRCS) $(OUTS)",
    output_to_bindir = True,
)

genrule(
    name = "build_model",
    srcs = glob(["model/*"]),
    outs = ["model"],
    cmd = "mkdir $(OUTS) && cp $(SRCS) $(OUTS)",
    output_to_bindir = True,
)

ts_project(
    name = "compile",
    srcs = glob(
        ["src/**/*"],
        exclude = [
            "src/**/*.test.*",
            "src/**/*.stories.*",
        ],
    ) + [
        ":build_api",
        ":build_model",
    ],
    allow_js = True,
    declaration = True,
    tsconfig = ":tsconfig.json",
    deps = [
        "@npm//:node_modules",
        "@npm//@types",
    ],
)

genrule(
    name = "copy_assets",
    srcs = glob(["assets/**/*"]),
    outs = ["assets"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

genrule(
    name = "embed_version",
    srcs = [
        "webpack.config.js",
        "webpack.common.js",
    ],
    outs = [
        "gen_webpack.config.js",
        "gen_webpack.common.js",
    ],
    cmd = """
    for f in $(SRCS)
    do
        sed -e "s/unknown_placeholder/$$(cat ./bazel-out/stable-status.txt | grep STABLE_VERSION | awk '{print $$2}')/g" $$f > $(@D)/gen_$$(basename $$f)
    done
    """,
    output_to_bindir = 1,
    stamp = True,
)

webpack(
    name = "public_files",
    args = [
        "--mode",
        "production",
        "--config",
        "$(execpath gen_webpack.config.js)",
        "--env htmlTemplate=./$(execpath base.html)",
        "--env bazelBinPath=./$(BINDIR)",
        "--output-path",
        "$(@D)",
        "./$(BINDIR)/pkg/app/web/src/index.js",
    ],
    configuration_env_vars = ["ENABLE_MOCK"],
    data = [
        "api_client",
        "base.html",
        "compile",
        "copy_assets",
        "embed_version",
        "gen_webpack.config.js",
        "gen_webpack.common.js",
        "model",
        "public/mockServiceWorker.js",
        "@npm//:node_modules",
    ] + glob(["assets/**/*"]),
    output_dir = True,
    visibility = ["//visibility:public"],
)

jest_test(
    name = "test",
    size = "small",
    srcs = glob([
        "src/**/*.ts",
        "src/**/*.snap",
    ]),
    jest_config = ":jest.config.js",
    deps = [
        ":build_api",
        ":build_model",
        ":custom-jsdom.js",
        ":file-transformer.js",
        ":jest.after-env.ts",
        ":jest.setup.js",
        ":test-utils",
        ":tsconfig.json",
        "@npm//:node_modules",
    ],
)
